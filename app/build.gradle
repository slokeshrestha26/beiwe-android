apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = rootProject.file("private/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "org.beiwe.app"
        minSdkVersion 16
        multiDexEnabled true
        targetSdkVersion 29
        versionCode 57       //TODO: update this Beiwe version code for new releases
        versionName '3.1.3'  //TODO: update this Beiwe version number for new releases
        setProperty("archivesBaseName", "Beiwe-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: ""]
            buildConfigField("boolean", "APP_IS_BETA", "false")
            buildConfigField("boolean", "APP_IS_DEV", "false")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['releaseDSN'] + "\"")
        }
        beta {
            versionNameSuffix("-beta")
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: "-beta"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "false")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['betaDSN'] + "\"")
        }
        development {
            versionNameSuffix("-development")
            signingConfig signingConfigs.config
            manifestPlaceholders = [appNameSuffix: "-development"]
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "true")
            buildConfigField("String", "SENTRY_DSN", "\"" + keystoreProperties['developmentDSN'] + "\"")
            debuggable true
        }
    }
    flavorDimensions "version"
    productFlavors {
        onnelaLabServer {
            dimension "version"
            manifestPlaceholders = [appName: "Beiwe"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", "false")
            buildConfigField("boolean", "READ_TEXTS_CALLS_BACKGROUND_LOCATION", 'true')
        }
        googlePlayStore {
            dimension "version"
            manifestPlaceholders = [appName: "Beiwe2"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", 'true')
            buildConfigField("boolean", "READ_TEXTS_CALLS_BACKGROUND_LOCATION", 'false')
        }
        commStatsCustomUrl {
            dimension "version"
            manifestPlaceholders = [appName: "Beiwe"]
            buildConfigField("boolean", "CUSTOMIZABLE_SERVER_URL", "true")
            buildConfigField("boolean", "READ_TEXTS_CALLS_BACKGROUND_LOCATION", 'true')
        }
    }
    useLibrary "org.apache.http.legacy"
    sourceSets {
        onnelaLabServer {
            manifest.srcFile 'src/textsAndCallsStats/AndroidManifest.xml'
        }
        commStatsCustomUrl {
            manifest.srcFile 'src/textsAndCallsStats/AndroidManifest.xml'
        }
    }
}
// Don't include the default "debug" build variant as an option
android.variantFilter { variant ->
    if (variant.buildType.name == 'debug') {
        variant.setIgnore(true)
    }
}

repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    mavenCentral()
}

dependencies {
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.commonsware.cwac:anddown:0.3.0'
    implementation 'io.sentry:sentry-android:1.7.3'
    implementation 'com.madgag.spongycastle:core:1.54.0.0'
    implementation 'com.google.firebase:firebase-messaging:20.2.3'
    implementation 'com.google.firebase:firebase-analytics:17.4.4'
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    apply plugin: 'kotlin-android-extensions'

}
